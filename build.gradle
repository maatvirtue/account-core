buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.4'
	}
}

apply plugin: 'com.bmuschko.tomcat'
apply plugin: 'war'

defaultTasks 'tasks'

group = 'net.maatvirtue'
version = '0.0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def versions = [
		spring : '4.2.5.RELEASE',
		jackson: '2.7.2',
		tomcat : '7.0.68'
]

dependencies {
	compile 'net.maatvirtue:common-lib:1.14.1'

	compile "org.springframework:spring-context-support:" + versions.spring
	compile "org.springframework:spring-tx:" + versions.spring
	compile "org.springframework:spring-webmvc:" + versions.spring
	compile "org.springframework.data:spring-data-jpa:1.9.4.RELEASE"
	compile("org.springframework:spring-context:" + versions.spring) {
		exclude group: 'commons-logging', module: 'commons-logging'
	}

	compile 'org.eclipse.persistence:javax.persistence:2.1.1'
	compile "org.flywaydb:flyway-core:4.0"
	compile 'org.hibernate:hibernate-validator:5.2.4.Final'

	compile 'javax.mail:javax.mail-api:1.5.5'
	compile 'javax.inject:javax.inject:1'

	compile 'javax.ws.rs:javax.ws.rs-api:2.0.1'
	compile 'org.apache.cxf:cxf-rt-frontend-jaxrs:3.1.6'

	compile 'org.slf4j:slf4j-api:1.7.18'
	compile 'ch.qos.logback:logback-classic:1.1.6'

	compileOnly 'javax.servlet:javax.servlet-api:3.1.0'
	compileOnly "org.apache.tomcat:tomcat-jdbc:${versions.tomcat}"

	runtime 'mysql:mysql-connector-java:5.1.38'
	runtime 'org.hibernate:hibernate-entitymanager:5.1.0.Final'

	tomcat "org.apache.tomcat.embed:tomcat-embed-core:${versions.tomcat}"
	tomcat "org.apache.tomcat.embed:tomcat-embed-jasper:${versions.tomcat}"
	tomcat "org.apache.tomcat:tomcat-jdbc:${versions.tomcat}"
	tomcat 'mysql:mysql-connector-java:5.1.38'
}

repositories {
	mavenCentral()
	mavenLocal()
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.12'
}

tomcatRun {
	httpPort = 8086
	stopPort = 8087
	stopKey = 'stop'
}

tomcatStop {
	stopPort = 8087
	stopKey = 'stop'
}


